/*
 * Copyright (C) 2016 Glucosio Foundation
 *
 * This file is part of Glucosio.
 *
 * Glucosio is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3.
 *
 * Glucosio is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Glucosio.  If not, see <http://www.gnu.org/licenses/>.
 *
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion buildToolsVer

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 41
        versionName '1.4.0'
        applicationId 'org.glucosio.android'

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true

            versionNameSuffix '-DEVEL'
            applicationIdSuffix '.daily'

            testCoverageEnabled true

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
            buildConfigField "String[]", "TRANSLATION_ARRAY", "new String[]{}"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField 'String', 'GOOGLE_ANALYTICS_TRACKER', '"UA-68882401-2"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String[]", "TRANSLATION_ARRAY", getAllLocales()
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            // configure the test JVM arguments
            jvmArgs '-noverify'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    wearApp project(':wear')

    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android Support libraries
    compile 'com.google.android.support:wearable:2.0.0'

    compile "com.android.support:appcompat-v7:${androidSupportVer}"
    compile "com.android.support:design:${androidSupportVer}"
    compile "com.android.support:cardview-v7:${androidSupportVer}"
    compile "com.android.support:recyclerview-v7:${androidSupportVer}"
    compile "com.android.support:percent:${androidSupportVer}"

    // Google Play Services
    compile "com.google.android.gms:play-services-analytics:${googlePlayServicesVer}"
    compile "com.google.android.gms:play-services-drive:${googlePlayServicesVer}"
    compile "com.google.android.gms:play-services-wearable:${googlePlayServicesVer}"
    compile "com.google.android.gms:play-services-gcm:${googlePlayServicesVer}"

    // Firebase
    compile "com.google.firebase:firebase-core:${googlePlayServicesVer}"
    compile "com.google.firebase:firebase-crash:${googlePlayServicesVer}"
    compile "com.google.firebase:firebase-invites:${googlePlayServicesVer}"
    compile "com.google.firebase:firebase-messaging:${googlePlayServicesVer}"

    // Other libraries
    compile 'com.github.paolorotolo:expandableheightlistview:1.0.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.wdullaer:materialdatetimepicker:2.4.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'net.danlew:android.joda:2.9.5.1'
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }

    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    compile files('libs/commons-math3-3.6.1.jar')

    // Butterknife
    compile "com.jakewharton:butterknife:${butterKnifeVer}"
    apt "com.jakewharton:butterknife-compiler:${butterKnifeVer}"

    testCompile "junit:junit:${junitVer}"
    testCompile "org.mockito:mockito-all:${mockitoVer}"
    testCompile "com.squareup.assertj:assertj-android:${androidAssertJVer}"
    testCompile("org.robolectric:robolectric:${robolectricVer}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile("org.robolectric:shadows-support-v4:${robolectricVer}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'io.reactivex:rxjava:1.2.4'

    testCompile "org.powermock:powermock-module-junit4:1.6.4"
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.4"
    testCompile "org.powermock:powermock-api-mockito:1.6.4"
    testCompile "org.powermock:powermock-classloading-xstream:1.6.4"

    // Testing libraries
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "com.android.support:support-annotations:${androidSupportVer}"
    androidTestCompile "com.android.support:recyclerview-v7:${androidSupportVer}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:${androidSupportVer}"
    resolutionStrategy.force "com.android.support:design:${androidSupportVer}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${androidSupportVer}"
}

def getAllLocales() {
    def foundLocales = new StringBuilder()
    foundLocales.append("new String[]{")

    fileTree("src/main/res").visit { FileVisitDetails details ->
        if (details.file.path.endsWith("strings.xml")) {
            def languageCode = details.file.parent.tokenize('/\\').last().replaceAll('values-', '').replaceAll('-r', '-')
            languageCode = (languageCode == "values") ? "en" : languageCode;
            foundLocales.append("\"").append(languageCode).append("\"").append(",")
        }
    }

    foundLocales.append("}")
    //Don't forget to remove the trailing comma
    return foundLocales.toString().replaceAll(',}', '}')
}

apply plugin: 'com.google.gms.google-services'

def coverageSourceDirs = ['src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'Reporting'

    description = 'Generate Jacoco coverage reports'

    classDirectories = fileTree(dir: 'build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewBinder*.*',
                       '**/*$InjectAdapter.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',])

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testDebugUnitTest.exec')

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    sourceDirs = files(['src/main/java']).files.absolutePath
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
}
